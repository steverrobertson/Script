% Config params, overwrite any previous settings from the commandline
config ap_ssid 		MQTT_SERVICES
config ap_password	stevieray01
config ssid		GlenVista1CF0
config password		stevieray01
config ntp_server	1.de.pool.ntp.org
config mqtt_host	m14.cloudmqtt.com
config mqtt_port	19801
config mqtt_user	htzkfqlx
config mqtt_password	PLsADzYDYbdX
%config speed		160

% Now the initialization, this is done once after booting
on init
do

	% Status of the relay
	setvar $relay_status=0
	setvar $run=0
	gpio_out 12 $relay_status
	gpio_out 13 not ($relay_status)

	% Voltage topic
	setvar $Voltage_topic="/system/voltage/VOLTS"

	% Season topic
	setvar $Season_topic="season/SET"
	setvar $Season="Summer"

	% Status topic
	setvar $status_topic="WaterHeater/PWR"

	publish local $status_topic $relay_status retained

	% local subscriptions once in 'init'
	subscribe local $Voltage_topic
	subscribe local $Season_topic
	
	settimer 1 500
	settimer 2 1000

% Now the MQTT client init, this is done each time the client connects
on mqttconnect
do
	% remote subscriptions for each connection in 'mqttconnect'
	publish remote $status_topic $relay_status retained

% Now the events, checked whenever something happens

on topic local $Season_topic
do
	if $Season = "Summer" then
		setvar $Season = "Winter"
		setvar $relay_status = 0
		gpio_out 12 $relay_status
		gpio_out 13 not ($relay_status)
	else
		setvar $Season = "Summer"
	endif
	publish local "stat/season" $Season
	publish remote "stat/season" $Season

on topic local $Voltage_topic
do
	setvar $Volts = $this_data
	setvar $int = csvstr($this_data, 0, ".")
	setvar $frac = csvstr($this_data, 1, ".") | "0"
	setvar $fixed_point = $int | substr($frac, 0, 1)
	
	if 542 > $fixed_point then
		setvar $run = 0
	endif
	if $Season = "Winter" then
		setvar $run=0
	endif
	if 510 > $fixed_point then
		setvar $relay_status = 0
		gpio_out 12 $relay_status
		gpio_out 13 not ($relay_status)
		publish local $status_topic "Off"
		publish remote $status_topic "Off"
	endif
	if $run > 4 then
		setvar $relay_status = 1
		gpio_out 12 $relay_status
		gpio_out 13 not ($relay_status)
		publish local $status_topic "On"
		publish remote $status_topic "On"
	endif
	% println "Minutes passed="|$run
	
% Checking timer
on timer 1
do
	setvar $run = $run + 1
	% println "Minutes passed="|$run
	if $run > 180 then
		setvar $run = 180
	endif
	settimer 1 60000

on timer 2
do
	if $Season = "Winter" then
		setvar $run=0
		setvar $relay_status = 0
		gpio_out 12 $relay_status
		gpio_out 13 not ($relay_status)
	endif
	publish local "stat/season" $Season
	publish remote "stat/season" $Season
	if $relay_status = 0 then
		publish local $status_topic "Off"
		publish remote $status_topic "Off"
	else
		publish local $status_topic "On"
		publish remote $status_topic "On"
	endif
	publish remote $Voltage_topic $Volts
	settimer 2 10000
